# epoll_ctl __________________________________________________
#
# long sys_epoll_ctl(int epfd, int op, int fd, struct epoll_event __user *event)
# long compat_sys_epoll_ctl(int epfd, int op, int fd,
#			struct compat_epoll_event __user *event)
#

@define _SYSCALL_EPOLL_CTL_NAME
%(
	name = "epoll_ctl"
%)

@define _SYSCALL_EPOLL_CTL_ARGSTR
%(
	argstr = sprintf("%d, %s, %d, %p", epfd, op_str, fd, event_uaddr)
%)

@define _SYSCALL_GATE
%(
%( arch == "s390" && kernel_v >= "3.15" %?
	# s390 switched from assembly compat wrappers to C-based
	# wrappers in kernel 3.15.
	@__syscall_gate_compat_simple
%)
%)

@define _SYSCALL_EPOLL_CTL_REGARGS
%(
	epfd = int_arg(1)
	op = int_arg(2)
	op_str = _opoll_op_str(op)
	fd = int_arg(3)
	event_uaddr = pointer_arg(4)
%)

@define _SYSCALL_EPOLL_CTL_REGARGS_STORE
%(
        if (@probewrite(epfd))
          set_int_arg(1, epfd)

	if (@probewrite(op))
	  set_int_arg(2, op)

	if (@probewrite(fd))
	  set_int_arg(3, fd)

	if (@probewrite(event_uaddr))
	  set_pointer_arg(4, event_uaddr)
%)

probe syscall.epoll_ctl = dw_syscall.epoll_ctl !, nd_syscall.epoll_ctl ? {}
probe syscall.epoll_ctl.return = dw_syscall.epoll_ctl.return !, nd_syscall.epoll_ctl.return ? {}

# dw_epoll_ctl _____________________________________________________

probe dw_syscall.epoll_ctl = @syscall_call("compat_sys_epoll_ctl"),
			     __syscall.epoll_ctl ?
{
	@_SYSCALL_EPOLL_CTL_NAME
	epfd = __int32($epfd)
	op = $op
	op_str = _opoll_op_str($op)
	fd = __int32($fd)
	event_uaddr = @__pointer($event)
	@_SYSCALL_EPOLL_CTL_ARGSTR
}
probe __syscall.epoll_ctl = @syscall_call("sys_epoll_ctl")
{
	@_SYSCALL_GATE
}
probe dw_syscall.epoll_ctl.return =
	@syscall_return("compat_sys_epoll_ctl"),
	__syscall.epoll_ctl.return ?
{
	@_SYSCALL_EPOLL_CTL_NAME
	@SYSC_RETVALSTR($return)
}
probe __syscall.epoll_ctl.return = @syscall_return("sys_epoll_ctl")
{
	@_SYSCALL_GATE
}

# nd_epoll_ctl _____________________________________________________

probe nd_syscall.epoll_ctl = nd1_syscall.epoll_ctl!, nd2_syscall.epoll_ctl!, tp_syscall.epoll_ctl
  { }

probe nd1_syscall.epoll_ctl = kprobe.function("compat_sys_epoll_ctl") ?,
                              __nd1_syscall.epoll_ctl ?
{
	@_SYSCALL_EPOLL_CTL_NAME
	asmlinkage()
	@_SYSCALL_EPOLL_CTL_REGARGS
	@_SYSCALL_EPOLL_CTL_ARGSTR
}
probe __nd1_syscall.epoll_ctl = kprobe.function("sys_epoll_ctl") ?
{
	@_SYSCALL_GATE
}

/* kernel 4.17+ */
probe nd2_syscall.epoll_ctl = kprobe.function(@arch_syscall_prefix "sys_epoll_ctl") ?
{
	__set_syscall_pt_regs(pointer_arg(1))
	@_SYSCALL_EPOLL_CTL_NAME
	@_SYSCALL_EPOLL_CTL_REGARGS
	@_SYSCALL_EPOLL_CTL_ARGSTR
},
{
        %( @_IS_SREG_KERNEL %? @_SYSCALL_EPOLL_CTL_REGARGS_STORE %)
}

/* kernel 3.5+, but undesirable because it affects all syscalls */
probe tp_syscall.epoll_ctl = kernel.trace("sys_enter")
{
	__set_syscall_pt_regs($regs)
	@__syscall_compat_gate(@const("__NR_epoll_ctl"), @const("__NR_compat_epoll_ctl"))
	@_SYSCALL_EPOLL_CTL_NAME
	@_SYSCALL_EPOLL_CTL_REGARGS
	@_SYSCALL_EPOLL_CTL_ARGSTR
},
{
        %( @_IS_SREG_KERNEL %? @_SYSCALL_EPOLL_CTL_REGARGS_STORE %)
}

probe nd_syscall.epoll_ctl.return = nd1_syscall.epoll_ctl.return!, nd2_syscall.epoll_ctl.return!, tp_syscall.epoll_ctl.return
  { }
  
probe nd1_syscall.epoll_ctl.return =
	kprobe.function("compat_sys_epoll_ctl").return ?,
	__nd1_syscall.epoll_ctl.return ?
{
	@_SYSCALL_EPOLL_CTL_NAME
	@SYSC_RETVALSTR(returnval())
}
probe __nd1_syscall.epoll_ctl.return = kprobe.function("sys_epoll_ctl").return ?
{
	@_SYSCALL_GATE
}

/* kernel 4.17+ */
probe nd2_syscall.epoll_ctl.return = kprobe.function(@arch_syscall_prefix "sys_epoll_ctl").return ?
{
	@_SYSCALL_EPOLL_CTL_NAME
	@SYSC_RETVALSTR(returnval())
}
 
/* kernel 3.5+, but undesirable because it affects all syscalls */
probe tp_syscall.epoll_ctl.return = kernel.trace("sys_exit")
{
	__set_syscall_pt_regs($regs)
	@__syscall_compat_gate(@const("__NR_epoll_ctl"), @const("__NR_compat_epoll_ctl"))
	@_SYSCALL_EPOLL_CTL_NAME
	@SYSC_RETVALSTR($ret)
}
